
% tokkie.pl, by Johan Bos

/* ========================================================================
   File Search Paths
======================================================================== */

file_search_path(semlib, 'src/prolog/lib').
file_search_path(boxer,  'src/prolog/boxer').


/* ========================================================================
   Dynamic Predicates
======================================================================== */

:- dynamic split/7, title/1.


/* ========================================================================
   Load other libraries
======================================================================== */

:- use_module(library(lists),[member/2,append/3,reverse/2]).
:- use_module(library(readutil),[read_stream_to_codes/2]).
:- use_module(semlib(abbreviations),[iAbb/2,tAbb/2]).
:- use_module(semlib(errors),[error/2,warning/2]).
:- use_module(semlib(options),[option/2,parseOptions/2,setOption/3,
                               showOptions/1,setDefaultOptions/1]).


/* ========================================================================
   Main
======================================================================== */

tokkie:-
   option(Option,do), 
   member(Option,['--help']), !, 
   help.

tokkie:-
   openInput(InStream),
   openOutput(OutStream), !,
   read_stream_to_codes(InStream,Codes),
   close(InStream),
   initTokkie,
   readLines(Codes,0,1,OutStream).

tokkie:-
   setOption(tokkie,'--help',do), !,
   help.


/* ----------------------------------------------------------------------
   Read lines
---------------------------------------------------------------------- */

readLines(Codes1,I1,S1,Stream):-
   begSent(Codes1,I1,Codes2,I2), !,       % determine begin of a new sentence
   endSent(Codes2,I2,Codes3,I3,Rest,[]),  % determine end of this sentence  
%  format(Stream,'sen(~p,~p,~s).~n',[I2,I3,Codes3]),
%  write(Codes3),nl,
   tokenise(Codes3,I2,I2,T-T,Tokens),     % split sentence into tokens
   printTokens(Tokens,S1,1,Stream),
   S2 is S1 + 1,                          % increase sentence counter
   readLines(Rest,I3,S2,Stream).          % process remaining of document

readLines(_,_,_,Stream):- close(Stream).


/* ----------------------------------------------------------------------
   Determine beginning of sentence
---------------------------------------------------------------------- */

begSent([Sep|C1],I1,C2,I3):- 
   sep(Sep), !,               % skip space, tab or newline
   I2 is I1 + 1,
   begSent(C1,I2,C2,I3).

begSent([C|L],I,[C|L],I).


/* ----------------------------------------------------------------------
   Determine end of sentence

   endSent(+CodesI,             % Input string
           +CurrentPosition,    % Current character position
           +CodesO,             % Output string (until sentence boundary)
           +BoundaryPosition,   % Character position of boundary
           +CodesR,             % Rest string
           +CodesLast)          % Last token

---------------------------------------------------------------------- */

endSent([],I,[],I,[],_):- !.

% Case 1: A full stop after a space 
%         --> sentence boundary.
endSent([46|Rest],I1,[46],I2,Rest,[]):- !, 
   I2 is I1 + 1.

% Case 2: full stop before a quote followed by a space
%         --> sentence boundary
endSent([46,Q1,Q2,X|Rest],I1,[46,Q1,Q2],I2,[X|Rest],_):- 
   \+ alphanum(X), quote(Q1), quote(Q2), !, I2 is I1 + 3.

endSent([46,Q,X|Rest],I1,[46,Q],I2,[X|Rest],_):- 
   \+ alphanum(X), quote(Q), !, I2 is I1 + 2.

% Case 3: full stop, but no sentence boundary
% 
endSent([C|C1],I1,[C|C2],I3,Rest,Last):- 
   noSentenceBoundary([C],C1,Last), !,
   I2 is I1 + 1,
   endSent(C1,I2,C2,I3,Rest,[C|Last]).

% Case 4: A full stop/question/exclemation mark after a non-abbreviation 
%         --> sentence boundary
endSent([End|Rest],I1,[End],I2,Rest,_):- 
   member(End,[46,63,33]), !, 
   I2 is I1 + 1.

endSent([46|Rest],I1,[46],I2,Rest,_):- !, 
   I2 is I1 + 1.

endSent([C|C1],I1,[C|C2],I3,Rest,Last):-
   alphanum(C), !,
   I2 is I1 + 1,
   endSent(C1,I2,C2,I3,Rest,[C|Last]).

endSent([C|C1],I1,[C|C2],I3,Rest,_):-
   I2 is I1 + 1,
   endSent(C1,I2,C2,I3,Rest,[]).


/* ----------------------------------------------------------------------
   Cases describing NO sentence boundaries

   noSentenceBoundary(Char,     % Character that could signal boundary
                      Next,     % Codes following
                      Last)     % Last token

---------------------------------------------------------------------- */
% Case 1: full stop after uppercase one-character token (i.e. initial)
noSentenceBoundary(".",_,Last):- Last = [Upper], upper(Upper).
% Case 2: full stop after a title 
noSentenceBoundary(".",_,Last):- title(Last).
% Case 2: full stop after an abbrev 
noSentenceBoundary(".",_,Last):- member(46,Last).
% Case 3: full stop before number
noSentenceBoundary(".",[N|_],_):- num(N).


/* ----------------------------------------------------------------------
   Split Line into Tokens
---------------------------------------------------------------------- */

% Nothing left to do, no tokens in queue
%
tokenise([],_,_,Sofar-[],[]):- Sofar=[], !.

% Nothing left to do, still a token present (input empty): store last token 
%
tokenise([],CurrentPos,StartPos,Sofar-[],[tok(StartPos,CurrentPos,Sofar)]):- !.

% Separator follows separator
%
tokenise([Sep|Codes],CurrentPos,_,T1-T2,Tokens):-
   sep(Sep), T2=[], T1=[], !,
   Pos is CurrentPos + 1, 
   tokenise(Codes,Pos,Pos,T-T,Tokens).

% Separator follows token
%
tokenise([Sep|Codes],CurrentPos,StartPos,Sofar-Tail,[Token|Tokens]):-
   sep(Sep), !, Tail = [],
   Token = tok(StartPos,CurrentPos,Sofar), 
   Pos is CurrentPos + 1, 
   tokenise(Codes,Pos,Pos,T-T,Tokens).

% Last character is a split, nothing in the queue: store last character
%
tokenise(Input,CurrentPos,_,Sofar-[],[Token|Tokens]):- 
   final(Input,Head,Rest,Len), Sofar = [], !,
   FinalPos is CurrentPos + Len,
   Token = tok(CurrentPos,FinalPos,Head),
   tokenise(Rest,FinalPos,FinalPos,T-T,Tokens).

% Last character is a split, store item in the queue and last character
%
tokenise(Input,CurrentPos,StartPos,Sofar-[],[Token1,Token2|Tokens]):- 
   final(Input,Head,Rest,Len), !,
   FinalPos is CurrentPos + Len,
   Token1 = tok(StartPos,CurrentPos,Sofar),
   Token2 = tok(CurrentPos,FinalPos,Head),
   tokenise(Rest,FinalPos,FinalPos,T-T,Tokens).

% Do not perform a split
%
tokenise(Input,CurrentPos,StartPos,OldSofar,Tokens):-
   dontsplit(Input,Rest,Diff,OldSofar,NewSofar), !,
   Pos is CurrentPos + Diff, 
   tokenise(Rest,Pos,StartPos,NewSofar,Tokens).


% Perform a token split operation
%
tokenise(Input,CurrentPos,StartPos,Sofar-Tail,[Token|Tokens]):-
   trysplit(Input,Left,Right,Rest,LenLeft,LenRight), !,
%format('Input: ~s~n',[Input]),
%format('Left: ~s~n',[Left]),
%format('Right: ~s~n',[Right]),
%format('Rest: ~s~n',[Rest]),
   Pos is CurrentPos + LenLeft,
   NewPos is Pos + LenRight,
   Tail = Left,
   Token = tok(StartPos,Pos,Sofar),    
   append(Right,NewTail,New),
   tokenise(Rest,NewPos,Pos,New-NewTail,Tokens).

% Do nothing but collect new token
%
tokenise([X|Codes],CurrentPos,StartPos,Sofar-Tail,Tokens):-
   Pos is CurrentPos + 1, 
   Tail = [X|NewTail],
   tokenise(Codes,Pos,StartPos,Sofar-NewTail,Tokens).


/* ----------------------------------------------------------------------
   Ouptut Tokens
---------------------------------------------------------------------- */

printTokens([],_,_,_). 

printTokens([tok(_,_,Tok)],_,_,Stream):- 
   option('--mode',poor), !,
   format(Stream,'~s~n',[Tok]). 

printTokens([tok(I,J,Tok)|L],S,T1,Stream):- 
   option('--format',prolog),
   option('--mode',rich), !,
   Index is S*1000+T1,
   format(Stream,'tok(~p, ~p, ~p, ~s).~n',[I,J,Index,Tok]), 
   T2 is T1+1,
   printTokens(L,S,T2,Stream).

printTokens([tok(I,J,Tok)|L],S,T1,Stream):- 
   option('--format',txt),
   option('--mode',rich), !,
   Index is S*1000+T1,
   format(Stream,'~p ~p ~p ~s~n',[I,J,Index,Tok]), 
   T2 is T1+1,
   printTokens(L,S,T2,Stream).

printTokens([tok(_,_,Tok)|L],S,T,Stream):- 
   option('--mode',poor), !,
   format(Stream,'~s ',[Tok]), 
   printTokens(L,S,T,Stream).


/* ----------------------------------------------------------------------
   Type checking
---------------------------------------------------------------------- */

sep(10).    % new line
sep(32).    % space
sep(9).     % tab
sep(160).   % nbsp (non-breaking space)
sep(8194).  % en space
sep(8195).  % em space

alphanum(X):- alpha(X), !.
alphanum(X):- num(X), !.

alpha(62):- !.                         %%% '>' (end of markup)
alpha(X):- upper(X), !.
alpha(X):- lower(X), !.

upper(X):- number(X), X > 64, X < 91, !.
upper(X):- var(X), member(X,"ABCDEFGHIJKLMNOPQRSTUVWXYZ").

lower(X):- number(X), X > 96, X < 123, !.
lower(X):- var(X), member(X,"abcdefghijklmnopqrstuvwxyz").

num(X):- number(X), X > 47, X < 58, !.
num(X):- var(X), member(X,"0123456789").


/* ----------------------------------------------------------------------
   Rules for splitting tokens
   split(+Left,+ConditionsOnLeft,+Right,+ConditionsOnRight,+Context)
---------------------------------------------------------------------- */

split("can",[], "not",[], []).
split([_],[], "n't",[], []).
split([_],[], "'ll",[], []).
split([_],[], "'ve",[], []).
split([_],[], "'re",[], []).

split([_],[], "'m",[], []).
split([_],[], "'d",[], []).
split([_],[], "'s",[], []).

split([N],[num(N)], ",",[], [32]).
split([A],[alpha(A)], [],[], ",").
split([_],[],         ";",[], []).
split([_],[],         ":",[], []).
split([_],[],         ")",[], []).
split([_],[],         "]",[], []).

split([N],[num(N)], "%",[],[]).

split("$",[],   [N],[num(N)], []).     % dollar
split([163],[], [N],[num(N)], []).     % pound
split([165],[], [N],[num(N)], []).     % yen
split("(",[],   [X],[alphanum(X)], []).
split("[",[],   [X],[alphanum(X)], []).

split([_],[],         [Q],[quote(Q)], []).
split([Q],[quote(Q)], [X],[alphanum(X)], []).


/* ----------------------------------------------------------------------
   Exceptions (do not split)
---------------------------------------------------------------------- */

dontsplit(Input,Rest,N,Old-OldTail,Old-NewTail):- 
   nosplit(Left,N),
   append(Left,Rest,Input), !,
   append(Left,NewTail,OldTail).

nosplit("hi'it",5).
nosplit("e.g.",4).
nosplit([79,Q,U],3):- rsq(Q), upper(U).   % Irish names


/* ----------------------------------------------------------------------
   Initialisation
---------------------------------------------------------------------- */

initTokkie:-  
   initTitles,
   initSplitRules.

initTitles:-
   option('--language',Language), !,
   findall(Title,
           ( tAbb(Language,Title),
             reverse(Title,Reversed),
             assertz(title(Reversed)) ),
           _).
          
initSplitRules:-
   findall(Ri,
          ( split(Le,CondLe,Ri,CondRi,Context),
            length(Le,LenLe),
            length(Ri,LenRi),
            assertz(split(Le,LenLe,CondLe,Ri,LenRi,CondRi,Context)) ),
          _).


/* ----------------------------------------------------------------------
   Rules for final tokens
---------------------------------------------------------------------- */

final("?", "?", [], 1).
final(".", ".", [], 1).

final([46,Q],[46], [Q],1):- quote(Q).


/* ----------------------------------------------------------------------
   Try a splitting rule on the input
---------------------------------------------------------------------- */

trysplit(Input,Left,Right,Rest,LenLeft,LenRight):-
   split(Left,LenLeft,CondsLeft,Right,LenRight,CondsRight,RightContext),
   append(Left,Middle,Input), 
   checkConds(CondsLeft),  
   append(Right,Rest,Middle), 
   checkConds(CondsRight),   
   append(RightContext,_,Rest), !.


/* ----------------------------------------------------------------------
   Check Conditions
---------------------------------------------------------------------- */

checkConds([]).
checkConds([C|L]):- call(C), !, checkConds(L).


/* ----------------------------------------------------------------------------------
   Dot dot dot (end of line)
   If the last token before the ... is an abbreviation, an extra . is preserved
---------------------------------------------------------------------------------- */

pattern(D-[], Prev, [46,32,46,46,46|B]-B, [46,46,46]):- dots(D,A), end(A), abb(Prev), !. 
pattern(D-[], Prev, B1-B2, [46,46,46]):- dots(D,A),end(A), !, insertSpace(Prev,[46,46,46|B2],B1).  

/* ----------------------------------------------------------------------------------
   Dot dot dot (not end of line)
---------------------------------------------------------------------------------- */

pattern(D-[L|A], Prev, B1-B2,[]):- dots(D,[L|A]), lower(L), !, insertSpace(Prev,[46,46,46,32|B2],B1).
pattern(D-[L|A], Prev, B1-B2,[]):- dots(D,[L|A]), upper(L), !, insertSpace(Prev,[46,46,46,10|B2],B1).
pattern(D-A,     Prev, B1-B2,[]):- dots(D,A), !, insertSpace(Prev,[46,46,46,32|B2],B1).

/* ----------------------------------------------------------------------------------
   Full stop and bracket (end of line)
---------------------------------------------------------------------------------- */

pattern([46,Q|A]-[], Prev, B1-B2, [Q]):- bracket(Q), end(A), !, insertSpace(Prev,[46,32,Q|B2],B1).   %%% X.) -> X . )

/* ----------------------------------------------------------------------------------
   Full stop and ending quotes (end of line)
---------------------------------------------------------------------------------- */

pattern([46,Q|A]-[], Prev, B1-B2, [46]):- quote(Q),end(A),option('--quotes',delete), !, insertSpace(Prev,[46|B2],B1).      %%% X." -> X .
pattern([46,Q|A]-[], Prev, B1-B2,  [Q]):- quote(Q),end(A),option('--quotes',keep), !, insertSpace(Prev,[46,32,Q|B2],B1).   %%% X." -> X . "

pattern([46,Q,Q|A]-[], Prev, B1-B2, [46]):- quotes(Q),end(A),option('--quotes',delete), !, insertSpace(Prev,[46|B2],B1).       %%% X.'' -> X .
pattern([46,Q,Q|A]-[], Prev, B1-B2,  [Q]):- quotes(Q),end(A),option('--quotes',keep), !, insertSpace(Prev,[46,32,Q,Q|B2],B1).  %%% X.'' -> X . ''

pattern([46,32,Q1,Q2|A]-[], Prev, B1-B2,  [46]):- quote(Q1),quote(Q2),\+Q1=Q2,end(A),option('--quotes',delete), !, insertSpace(Prev,[46|B2],B1).            %%% X. '" -> X . ' "
pattern([46,32,Q1,Q2|A]-[], Prev, B1-B2,  [Q2]):- quote(Q1),quote(Q2),\+Q1=Q2,end(A),option('--quotes',keep), !, insertSpace(Prev,[46,32,Q1,32,Q2|B2],B1).  %%% X. '" -> X . ' "

/* ----------------------------------------------------------------------------------
   Full stop and ending quotes (not end of line)
---------------------------------------------------------------------------------- */

pattern([46,Q,32,U|A]-[U|A], Prev, B1-B2, []):- quote(Q),upper(U),option('--quotes',delete), !, insertSpace(Prev,[46,10|B2],B1). %%% X." U
pattern([46,Q,32,U|A]-[U|A], Prev, B1-B2, []):- quote(Q),upper(U),option('--quotes',keep), !, insertSpace(Prev,[46,32,Q,10|B2],B1). %%% X." U
pattern([46,Q,32,U|A]-[U|A], Prev, B1-B2, []):- closing_bracket(Q),upper(U), !, insertSpace(Prev,[46,32,Q,10|B2],B1). %%% X.) U

/* ----------------------------------------------------------------------------------
   Full stop (end of line)
   If the last token before the . is an abbreviation, no extra . is produced.
---------------------------------------------------------------------------------- */

pattern([46|A]-[], Prev, [46|B]-B, Prev):- end(A), title(Prev), !.                   %%% X. -> X. 
pattern([46|A]-[], Prev, [46|B]-B, [46|Prev]):- end(A), abb(Prev), !.                %%% X. -> X. 
pattern([46|A]-[], Prev, B1-B2, [46]):- end(A), !, insertSpace(Prev,[46|B2],B1).     %%% X. -> X . 

/* ----------------------------------------------------------------------------------
   Full stop, followed by opening quote
---------------------------------------------------------------------------------- */

pattern([46,32,Q,115|A]-A, [_|_], [46,32,Q,115|B]-B, [115,Q]):- rsq(Q), !.   %% U.S. \'s
pattern([46,32,Q,C|A]-[Q,C|A],     Prev, B1-B2, []):- quote(Q), upper(C), !, insertSpace(Prev,[46,10|B2],B1).
pattern([46,32,Q,Q,C|A]-[Q,Q,C|A], Prev, B1-B2, []):- quotes(Q), upper(C), !, insertSpace(Prev,[46,10|B2],B1).
pattern([46,32,Q,C|A]-[Q,C|A],     Prev, B1-B2, []):- opening_bracket(Q), upper(C), !, insertSpace(Prev,[46,10|B2],B1).

/* ----------------------------------------------------------------------------------
   Full stop (not end of line), next token starts with uppercase --- arhhhhh....
   Case 1: A full stop after a space -> sentence boundary.
   Case 2: A full stop after a one-character token --> initial, no sentence boundary
   Case 3: A full stop after a title --> no sentence boundary
   Case 4: A full stop after a non-abbreviation --> sentence boundary
%  Case 5: A full stop after abbreviation --> no sentence boundary
---------------------------------------------------------------------------------- */

pattern([46,32,U|A]-[U|A], [], [46,10|B]-B,   []):- upper(U), !.
pattern([46,32,U|A]-[U|A], [_], [46,32|B]-B,  []):- upper(U), !.    %%% Initial
pattern([46,32,U|A]-[U|A], Prev, [46,32|B]-B, []):- upper(U), title(Prev), !.
pattern([46,32,U|A]-[U|A], Prev, [32,46,10|B]-B, []):- upper(U), \+ abb(Prev), !.
%pattern([46,32,U|A]-[U|A], Prev, [46,10|B]-B, []):- upper(U), abb(Prev), !.

pattern([46,32,32,U|A]-[U|A], [], [46,10|B]-B,   []):- upper(U), !.
pattern([46,32,32,U|A]-[U|A], [_], [46,32|B]-B,  []):- upper(U), !.    %%% Initial
pattern([46,32,32,U|A]-[U|A], Prev, [46,32|B]-B, []):- upper(U), title(Prev), !.
pattern([46,32,32,U|A]-[U|A], Prev, [32,46,10|B]-B, []):- upper(U), \+ abb(Prev), !.

/* ----------------------------------------------------------------------------------
   The brackets
---------------------------------------------------------------------------------- */

pattern([X|A]-[32|A], Prev, B1-B2, [X]):- bracket(X), !, insertSpace(Prev,[X|B2],B1).


/* ----------------------------------------------------------------------------------
   Question and Exclamation Mark
---------------------------------------------------------------------------------- */

pattern([X|A]-[32|A], Prev, B1-B2, [X]):- mark(X), !, insertSpace(Prev,[X|B2],B1).


/* ----------------------------------------------------------------------------------
   Contractions: year/decade expressions
---------------------------------------------------------------------------------- */

pattern([Q,N1,N2,115|A]-A, [], [Q,N1,N2,115|B]-B, [115,N2,N1,Q]):- rsq(Q), num(N1),num(N2), !.  %%% "'30s" -> "'30s"
pattern([Q,N1,N2,N|A]-[N|A], [], [Q,N1,N2|B]-B, [N2,N1,Q]):- rsq(Q), num(N1),num(N2), \+ alphanum(N), !.  %%% "'30" -> "'30"


/* ----------------------------------------------------------------------------------
   Contractions (Italian)
---------------------------------------------------------------------------------- */

pattern([108,Q,X|A]-[X|A],   Prev, B1-B2, []):- option('--language',it), alpha(X), rsq(Q), !, insertSpace(Prev,[108,Q,32|B2],B1).   %%% " l'X" -> " l' X"


/* ----------------------------------------------------------------------------------
   Contractions: Irish and foreign names
---------------------------------------------------------------------------------- */

pattern([U1,Q,U2|A]-A, [], [U1,Q,U2|B]-B, [U2,Q,U1]):- rsq(Q), alpha(U1),alpha(U2).  %%% "O'R" -> "O'R"

/* ----------------------------------------------------------------------------------
   Double character quotes
---------------------------------------------------------------------------------- */

pattern([32,Q,Q,32|A]-[32|A], X, B-B, X):- quotes(Q), option('--quotes',delete), !.
pattern([Q,Q|A]-A, X, B-B, X):- quotes(Q), option('--quotes',delete), !.
pattern([X,X|A]-[32|A], Prev, B1-B2, [X,X]):- quotes(X), !, insertSpace(Prev,[X,X|B2],B1).

/* ----------------------------------------------------------------------------------
   Single character quotes
---------------------------------------------------------------------------------- */

pattern([32,Q,32|A]-[32|A], X, B-B, X):- quote(Q), option('--quotes',delete), !.
pattern([Q|A]-A, X, B-B, X):- quote(Q), option('--quotes',delete), !.
pattern([X|A]-[32|A], Prev, B1-B2, [X]):- quote(X), !, insertSpace(Prev,[X|B2],B1).   


/* ----------------------------------------------------------------------------------
   Insert space, but only if there is a token just before

insertSpace([], L, L):- !.
insertSpace( _, L, [32|L]).
---------------------------------------------------------------------------------- */


/* ----------------------------------------------------------------------------------
   Codes for Brackets
---------------------------------------------------------------------------------- */

bracket(X):- opening_bracket(X).
bracket(X):- closing_bracket(X).

opening_bracket(40).  %%% (
opening_bracket(91).  %%% [
opening_bracket(123). %%% {

closing_bracket(41).  %%% )
closing_bracket(93).  %%% ]
closing_bracket(125). %%% }


/* ----------------------------------------------------------------------------------
   Codes for right single quotation marks (used in genitives)
---------------------------------------------------------------------------------- */

rsq(39).
rsq(8217).


/* ----------------------------------------------------------------------------------
   Codes for single-character quotes
---------------------------------------------------------------------------------- */

quote(34).    %%% "
quote(39).    %%% '
quote(96).    %%% `
quote(8216).  %%% left single quotation mark
quote(8217).  %%% right single quotation mark
quote(8218).  %%% low single quotation mark
quote(8220).  %%% left double quotation mark
quote(8221).  %%% right double quotation mark
quote(8222).  %%% low double quotation mark


/* ----------------------------------------------------------------------------------
   Codes for double quotes
---------------------------------------------------------------------------------- */

quotes(96).    %%% ``
quotes(39).    %%% ''
quotes(8216).
quotes(8217).
quotes(8218).

/* ----------------------------------------------------------------------------------
   Codes for punctuation marks
---------------------------------------------------------------------------------- */

mark(63).    %%% ?
mark(33).    %%% !



/* =======================================================================
   Open Input File
========================================================================*/

openInput(Stream):-
   option('--stdin',dont),
   option('--input',File),
   exists_file(File), !,
   open(File,read,Stream,[encoding(utf8)]).

openInput(Stream):-
   option('--stdin',do), 
   set_prolog_flag(encoding,utf8),
   warning('reading from standard input',[]),
   prompt(_,''),
   Stream = user_input.


/* =======================================================================
   Open Output File
========================================================================*/

openOutput(Stream):-
   option('--output',Output),
   atomic(Output),
   \+ Output=user_output,
   ( access_file(Output,write), !,
     open(Output,write,Stream,[encoding(utf8)])
   ; error('cannot write to specified file ~p',[Output]),
     Stream=user_output ), !.

openOutput(user_output).


/* =======================================================================
   Help
========================================================================*/

help:-
   option('--help',do), !,
   format(user_error,'usage: tokkie [options]~n~n',[]),
   showOptions(tokkie).

help:-
   option('--help',dont), !.


/* =======================================================================
   Definition of start
========================================================================*/

start:-
   current_prolog_flag(argv,[_Comm|Args]),
   setDefaultOptions(tokkie), 
   parseOptions(tokkie,Args),
   tokkie, !,
   halt.

start:- 
   error('tokkie failed',[]), 
   halt.


